rule all_PIPITS:
  input:
   TARGET_PIPITS

rule createreadpairslist:
  input:
    expand(TRIM_DIR + "/{sample}_{direction}_vsearch_trimmed.fastq.gz", sample = SAMPLE_IDS, direction = ["R1", "R2"])
  params:
    TRIM_DIR
  output:
    PIPITS_DIR + "/readpairslist.txt"
  shell:
    """
    pispino_createreadpairslist -i {params} -o {output}
    """

rule seqprep:
  input:
    PIPITS_DIR + "/readpairslist.txt"
  params:
    trim_dir = TRIM_DIR,
    out_seqprep = PIPITS_DIR + "/out_seqprep"
  threads:
    config["all"]["threads"]
  output:
    PIPITS_DIR + "/out_seqprep/prepped.fasta"
  shell:
    """
    pispino_seqprep -i {params.trim_dir} -o {params.out_seqprep} -l {input} -t {threads}
    """

rule funits:
  input:
    PIPITS_DIR + "/out_seqprep/prepped.fasta"
  params:
    output_dir = PIPITS_DIR + "/out_funits",
    its_subregion = config["all"]["ITS_subregion"]
  threads:
    config["all"]["threads"]
  output:
    PIPITS_DIR + "/out_funits/ITS.fasta"
  shell:
    """
    pipits_funits -i {input} -o {params.output_dir} -t {threads} -x {params.its_subregion}
    """

rule process:
  input:
    PIPITS_DIR + "/out_funits/ITS.fasta" if config["all"]["ITS_subregion"] else PIPITS_DIR + "/out_seqprep/prepped.fasta"
  params:
    PIPITS_DIR + "/out_process"
  threads:
    config["all"]["threads"]
  output:
    PIPITS_DIR + "/out_process/repseqs.fasta"
  shell:
    """
    pipits_process -i {input} -o {params} -t {threads} --Xmx 4G
    """
